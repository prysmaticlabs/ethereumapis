##############################################################################
# Common
##############################################################################

proto_library(
    name = "proto",
    srcs = [
        "attestation.proto",
        "beacon_block.proto",
        "beacon_chain.proto",
        "node.proto",
        "validator.proto",
        ":generated_swagger_proto",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "@grpc_ecosystem_grpc_gateway//protoc-gen-swagger/options:options_proto",
        "@com_google_protobuf//:empty_proto",
        "@com_google_protobuf//:timestamp_proto",
        "@go_googleapis//google/api:annotations_proto",
    ],
)

##############################################################################
# Java
##############################################################################

java_proto_library(
    name = "java_proto",
    deps = [":proto"],
)

##############################################################################
# Go
##############################################################################
# gazelle:ignore
load("@io_bazel_rules_go//go:def.bzl", "go_library")
load("@io_bazel_rules_go//proto:def.bzl", "go_proto_library")

go_proto_library(
    name = "go_proto",
    compilers = ["@io_bazel_rules_go//proto:go_grpc"],
    importpath = "github.com/prysmaticlabs/ethereumapis/eth/v1alpha1",
    proto = ":proto",
    visibility = ["//visibility:public"],
    deps = [
        "@grpc_ecosystem_grpc_gateway//protoc-gen-swagger/options:options_go_proto",
        "@go_googleapis//google/api:annotations_go_proto",
    ],
)

go_library(
    name = "go_default_library",
    embed = [":go_proto"],
    importpath = "github.com/prysmaticlabs/ethereumapis/eth/v1alpha1",
    visibility = ["//visibility:public"],
)

##############################################################################
# OpenAPI (Swagger) V2
##############################################################################
load("@grpc_ecosystem_grpc_gateway//protoc-gen-swagger:defs.bzl", "protoc_gen_swagger")

protoc_gen_swagger(
    name = "swagger",
    proto = ":proto",
    visibility = ["//visibility:public"],
    single_output = True,
    json_names_for_fields = True,
)

# Genrule for template subsitution.
# See documentation in //tools/replacer.
genrule(
    name = "generated_swagger_proto",
    srcs = [
        "swagger.proto", # A go template compatibile file.
        "swagger_description.md", # Replacement for description. 
    ],
    outs = ["swagger_generated.proto"],
    cmd = "$(location //tools/replacer) $(location swagger.proto) description=$(location swagger_description.md) > $(@)",
    tools = ["//tools/replacer"],
)
