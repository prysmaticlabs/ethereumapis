package(default_visibility = ["//visibility:public"])

##############################################################################
# Common
##############################################################################
load("@rules_proto//proto:defs.bzl", "proto_library")

proto_library(
    name = "proto",
    srcs = [
        "attestation.proto",
        "beacon_block.proto",
        "beacon_chain.proto",
        "node.proto",
        "validator.proto",
        ":generated_swagger_proto",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "@com_google_protobuf//:empty_proto",
        "@com_google_protobuf//:any_proto",
        "@com_google_protobuf//:timestamp_proto",
        "@go_googleapis//google/api:annotations_proto",
        "@gogo_special_proto//github.com/gogo/protobuf/gogoproto",
        "@com_github_grpc_ecosystem_grpc_gateway//protoc-gen-swagger/options:options_proto",
    ],
)

##############################################################################
# Java
##############################################################################
load("@rules_java//java:defs.bzl", "java_proto_library")

java_proto_library(
    name = "java_proto",
    deps = [":proto"],
)

##############################################################################
# Go
##############################################################################
# gazelle:ignore
load("@io_bazel_rules_go//go:def.bzl", "go_library")
load("@io_bazel_rules_go//proto:def.bzl", "go_proto_library")
load("//tools:ssz.bzl", "SSZ_DEPS", "ssz_gen_marshal")

ssz_gen_marshal(
    name = "ssz_generated_files",
    go_proto = ":go_proto",
    objs = [
        "Attestation",
        "BeaconBlock",
        "SignedBeaconBlock",
        "Validator",
        "BeaconBlockHeader",
        "AttesterSlashing",
        "VoluntaryExit",
        "Deposit",
        "ProposerSlashing",
    ],
    visibility = ["//visibility:private"],
)

go_proto_library(
    name = "go_proto",
    compilers = ["@io_bazel_rules_go//proto:gogofast_grpc"],
    importpath = "github.com/prysmaticlabs/ethereumapis/eth/v1alpha1",
    proto = ":proto",
    visibility = ["//visibility:public"],
    deps = [
        "@go_googleapis//google/api:annotations_go_proto",
        "@com_github_grpc_ecosystem_grpc_gateway//protoc-gen-swagger/options:options_go_proto",
        "@com_github_prysmaticlabs_go_bitfield//:go_default_library",
    ],
)

go_proto_library(
    name = "go_grpc_gateway_library",
    compilers = [
        "@io_bazel_rules_go//proto:go_grpc",
        "@com_github_grpc_ecosystem_grpc_gateway//protoc-gen-grpc-gateway:go_gen_grpc_gateway",
    ],
    importpath = "github.com/prysmaticlabs/ethereumapis/eth/v1alpha1_gateway",
    proto = ":proto",
    visibility = ["//visibility:public"],
    deps = [
        "@go_googleapis//google/api:annotations_go_proto",
        "@com_github_grpc_ecosystem_grpc_gateway//protoc-gen-swagger/options:options_go_proto",
        "@com_github_gogo_protobuf//gogoproto:go_default_library",
        "@com_github_golang_protobuf//descriptor:go_default_library",
        "@com_github_golang_protobuf//ptypes/empty:go_default_library",
        "@com_github_prysmaticlabs_go_bitfield//:go_default_library",
    ],
)

go_library(
    name = "go_default_library",
    srcs = [":ssz_generated_files"],
    embed = [":go_proto"],
    importpath = "github.com/prysmaticlabs/ethereumapis/eth/v1alpha1",
    visibility = ["//visibility:public"],
    deps = SSZ_DEPS,
)


##############################################################################
# OpenAPI (Swagger) V2
##############################################################################
load("@com_github_grpc_ecosystem_grpc_gateway//protoc-gen-swagger:defs.bzl", "protoc_gen_swagger")
protoc_gen_swagger(
    name = "swagger",
    json_names_for_fields = True,
    proto = ":proto",
    single_output = True,
    visibility = ["//visibility:public"],
)

# Genrule for template subsitution.
# See documentation in //tools/replacer.
genrule(
    name = "generated_swagger_proto",
    srcs = [
        "swagger.proto",  # A go template compatibile file.
        "swagger_description.md",  # Replacement for description.
    ],
    outs = ["swagger_generated.proto"],
    cmd = "$(location //tools/replacer) $(location swagger.proto) description=$(location swagger_description.md) > $(@)",
    tools = ["//tools/replacer"],
    visibility = ["//visibility:private"],
)

##############################################################################
# JavaScript / TypeScript
##############################################################################
load("@npm_bazel_labs//:index.bzl", "ts_proto_library")

ts_proto_library(
    name = "ts_library",
    proto = ":proto",
)